## LTLf

In this section, we specify a grammar for LTLf.

### Atoms

An LTLf formula is defined over a set of _atoms_.
In this context, an atom formula is defined by using 
the `Atom` regular language defined above:

```
LTLAtom ::= Atom
```

### Temporal operators

Here we specify the regular languages for the temporal operators.

- (Weak) Next: `X`;
- Strong Next: `X[!]`;
- (Strong) Until: `U`;
- Weak Until: `W`;
- (Weak) Release: `R`, `V`;
- Strong Release: `M`;
- Eventually: `F`;
- Always: `G`;

In EBNF format:
```
WeakNext      ::= "X"
Next          ::= "X[!]"
Until         ::= "U"
WeakUntil     ::= "W"
Release       ::= "R" | "V"
StrongRelease ::= "M"
Eventually    ::= "F"
Always        ::= "G"
```

### Special Formulae

Special LTLf formulae are:

- `last`, meaning "the last step of the trace", semantically equivalent to `X(false)`;
- `end`, meaning "the end of the trace", semantically equivalent to `G(false)`.

In EBNF format:
```
Last ::= "last"
End  ::= "end"
```

### Grammar

```
ltl_formula ::= LTLAtom
                | PropBooleans
                | LogicBooleans
                | Last
                | End
                | LeftParen ltl_formula RightParen 
                | Not ltl_formula 
                | ltl_formula And ltl_formula
                | ltl_formula Or ltl_formula
                | ltl_formula Impl ltl_formula
                | ltl_formula Equiv ltl_formula
                | ltl_formula Xor ltl_formula
                | ltl_formula Until ltl_formula
                | ltl_formula WeakUntil ltl_formula
                | ltl_formula Release ltl_formula
                | ltl_formula StrongRelease ltl_formula
                | Eventually ltl_formula
                | Always ltl_formula
                | WeakNext ltl_formula
                | Next ltl_formula
```

For the semantics of these operators,
we refer to [@degiacomo2013]
for the finite setting.


### Precedence and associativity of operators

The precedence and associativity of the LTL operators are 
described by the following table (priorities from lowest to highest).
For brevity, aliases for boolean operators
are omitted.

<center>

|associativity|operators|
|-|-|
|right|`->`, `<->`|
|left|`^`|
|left|`|`|
|left|`&`|
|right |`U`,`W`,`M`,`R`|
|right |`F`, `G`|
|right |`X`, `X[!]`|
|right |`!`, `~`|
</center>


