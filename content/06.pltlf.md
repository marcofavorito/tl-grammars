## PLTLf

In this section, we specify a grammar for PLTLf.

### Atoms

A PLTLf formula is defined over a set of _atoms_.
In this context, an atom formula is defined by using 
the `Atom` regular language defined above:

```
PLTLAtom ::= Atom
```

### Temporal operators

Here we specify the regular languages for the temporal operators.

- Before: `Y`;
- Since: `S`;
- Once: `O`;
- Historically `H`

In EBNF format:
```
Before       ::= "Y"
Since        ::= "S"
Once         ::= "O"
Historically ::= "H"
```


### Special Formulae

Special PLTLf formulae are:

- `first`, meaning "the first step of the trace", semantically equivalent to `!B(true)`;
- `start`, meaning "the start of the trace", semantically equivalent to `H(false)`.

In EBNF format:
```
First ::= "first"
Start ::= "start"
```


### Grammar

```
pltl_formula ::= PLTLAtom
                 | PropBooleans
                 | LogicBooleans
                 | First
                 | Start
                 | LeftParen pltl_formula RightParen 
                 | Not pltl_formula 
                 | pltl_formula And pltl_formula
                 | pltl_formula Or pltl_formula
                 | pltl_formula Impl pltl_formula
                 | pltl_formula Equiv pltl_formula
                 | pltl_formula Xor pltl_formula
                 | pltl_formula Since pltl_formula
                 | Once pltl_formula
                 | Historically pltl_formula
                 | Before pltl_formula
```

For the semantics of these operators for the finite setting,
we refer to [@degiacomo2020].


### Precedence and associativity of operators

The precedence and associativity of the LTL operators are 
described by the following table (priorities from lowest to highest).
For brevity, aliases for boolean operators
are omitted.

<center>

|associativity|operators|
|-|-|
|right|`->`, `<->`|
|left|`^`|
|left|`|`|
|left|`&`|
|right |`S`|
|right |`O`, `H`|
|right |`B`|
|right |`!`|
</center>


