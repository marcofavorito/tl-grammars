## LDLf

In this section, we specify a grammar for LDLf.

### Temporal operators

LDLf supports two temporal operators:

- _Diamond_ operator: `<regex>ldl_formula`;
- _Box_ operator: `[regex]ldl_formula`;

`regex` will be presented in the next paragraph.

```
LeftDiam  ::= "<"
RightDiam ::= ">"
LeftBox   ::= "["
RightBox  ::= "]"
```

In EBNF format, an LDLf formula is defined as follows:

```
ldl_formula ::= TT
              | FF
              | LeftParen ldl_formula RightParen
              | Not ldl_formula 
              | ldl_formula And ldl_formula
              | ldl_formula Or ldl_formula
              | ldl_formula Impl ldl_formula
              | ldl_formula Equiv ldl_formula
              | LeftDiam regex RightDiam ldl_formula
              | LeftBox regex RightBox ldl_formula
```

### Regular Expressions

In this section, we define the regular expression used by 
Diamond and Box operators.

A regular expression is defined inductively as:

- a _propositonal formula_ over as set of propositional atoms.
- a _test expression_: `ldl_formula?
- a _concatenation_ between two regular expressions: `regex_1 ; regex_2`
- a _union_ between two regular expressions: `regex_1 + regex_2`
- a _star_ operator over a regular expression: `regex*`

The symbols are listed below:

```
Test   ::= "?"
Concat ::= ";"
Union  ::= "+"
Star   ::= "*"
```

The EBNF grammar for a regular expression is:

```
propositional ::= Atom
                  | True
                  | False
                  | LeftParen propositional RightParen 
                  | Not propositional 
                  | propositional And propositional
                  | propositional Or propositional
                  | propositional Impl propositional
                  | propositional Equiv propositional
                  | propositional Xor propositional

regex ::= propositional
          | LeftParen regex RightParen
          | regex Test
          | regex Concat regex 
          | regex Union regex 
          | regex Star
``` 

For the semantics of the operators,
we refer to [@degiacomo2013].


### Precedence and associativity of operators

The precedence and associativity of the LDL operators are 
described by the following table (priorities from lowest to highest).
For brevity, aliases for boolean operators
are omitted.

<center>

|associativity|operators|
|-|-|
|right|`->`, `<->`|
|left|`^`|
|left|`|`|
|left|`&`|
|N/A|`<>`, `[]`|
|left|`;`|
|left|`+`|
|left|`*`|
|left|`?`|
|right|`!`, `~`|
</center>

